buildscript {
  ext {
    springBootVersion = '2.0.8.RELEASE'
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
  }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'checkstyle'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'findbugs'

checkstyle {
  toolVersion '8.17'
  configFile file("config/checkstyle/checkstyle.xml")
}
checkstyleMain {
  source ='src/main/java'
}
checkstyleTest {
  source ='src/test/java'
}

group = 'groupthree'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
  mavenCentral()
}

ext {
  jasperVersion = '9.0.10'
  groovyVersion = '2.4.15'
  spockVersion = '1.1-groovy-2.4'
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  runtimeOnly 'org.springframework.boot:spring-boot-devtools'
  runtimeOnly 'com.h2database:h2'
  runtimeOnly 'mysql:mysql-connector-java'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  
  //OAuth2
  implementation 'org.springframework.security:spring-security-oauth2-client'
  implementation 'org.springframework.security:spring-security-oauth2-jose'
  compile 'org.springframework.security:spring-security-core'
  
  //JSP
  compile("javax.servlet:jstl:1.2")
  compile("org.apache.tomcat.embed:tomcat-embed-jasper:$jasperVersion")
  
  // Spring
  testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
  // Groovy
  testCompile("org.codehaus.groovy:groovy-all:$groovyVersion")
  // Spock
  testCompile("junit:junit:4.12")
  testCompile("org.spockframework:spock-core:$spockVersion")
  testCompile "org.spockframework:spock-spring:$spockVersion"
  testCompile("org.hamcrest:hamcrest-core:1.3")
  testCompile("org.hamcrest:hamcrest-library:1.3")
  // Spock-reports
  testCompile( 'com.athaydes:spock-reports:1.3.1' ) {
    transitive = false // this avoids affecting your version of Groovy/Spock
  }
}

jacocoTestCoverageVerification {
  violationRules {
      rule {
          limit {
              minimum = 0.8
          }
      }
  }
}

jacocoTestReport {
  reports {
    html.enabled true
  }

  afterEvaluate {
    classDirectories = files(classDirectories.files.collect {
      fileTree(dir: it, exclude: [
        '**/*GitrulerApplication**',
        '**/*SecurityConfig**'
      ])
    })
  }
}

check.dependsOn jacocoTestCoverageVerification
test.finalizedBy(project.tasks.jacocoTestReport)

findbugs{ findbugsTest.enabled=false }

tasks.withType(FindBugs) {
  reports {
      xml.enabled = false
      html.enabled = true
  }
}
